managed;
strict ( 2 );

with draft;

define behavior for ZI_RAP_ORDER_DG alias Order
implementation in class zbp_i_rap_order_dg unique
persistent table zrap_aorder_dg
draft table zrap_dorder_dg
lock master total etag LastChangedAt
authorization master ( instance )
etag master LastChangedAt
{
  action ( features : instance ) setStatusCancelled result [1] $self;
  action ( features : instance ) setStatusCompleted result [1] $self;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;

  internal action calculateTotalPrice;


  create;
  update ( features : instance );
  delete ( features : instance );

  association _Item { create ( features : instance ); with draft; }

  field ( numbering : managed, readonly ) Orderuuid;
  field ( readonly ) OrderId, Status, TotalPrice;
  field ( readonly ) CreationDate, CancellationDate, CompletionDate, LastChangedAt;
  field ( mandatory ) Name, CustomerId, DeliveryCountry, CurrencyCode;

  determination setInitialStatus on modify { create; }
  determination setCreationDate on modify { create; }
  determination calculateOrderID on save { create; }
  determination propagateCurrencyCode on modify { field CurrencyCode; }


  validation validateCustomer on save { field CustomerID; create; }
  validation validateDeliveryCountry on save { field DeliveryCountry; create; }
  validation validateOrderName on save { field Name; create; }
  validation validateCurrencyCode on save { field CurrencyCode; create; update; }

  validation checkHasItems on save { create; update; }

  side effects { field CurrencyCode affects field _Item.CurrencyCode; }

  draft determine action Prepare
  {
    validation validateDeliveryCountry;
    validation validateCustomer;
    validation validateOrderName;
    validation validateCurrencyCode;
    validation checkHasItems;
    validation Item~validateQuantity;
    validation Item~validatePrice;
    validation Item~validateItemName;
  }

  mapping for zrap_aorder_dg
    {
      Orderuuid        = orderuuid;
      Orderid          = orderid;
      Name             = name;
      Status           = status;
      CustomerId       = customer_id;
      CreationDate     = creation_date;
      CancellationDate = cancellation_date;
      CompletionDate   = completion_date;
      DeliveryCountry  = delivery_country;
      CurrencyCode     = currency_code;
      TotalPrice       = total_price;
      LastChangedAt    = last_changed_at;
    }
}


define behavior for ZI_RAP_ITEM_DG alias Item
implementation in class zbp_i_rap_item_dg unique
persistent table zrap_aitem_dg
draft table zrap_ditem_dg
lock dependent by _Order
authorization dependent by _Order
etag master LastChangedAt
{
  update ( features : instance );
  delete ( features : instance, precheck );

  association _Order { with draft; }

  field ( numbering : managed, readonly ) Itemuuid;
  field ( readonly ) Orderuuid, LastChangedAt;
  field ( mandatory ) Name, Price, Quantity;
  field ( features : instance ) CurrencyCode;

  determination calculateOnModify on modify { field Quantity, Price; }
  determination calculateOnDelete on modify { delete; }
  determination initCurrencyCode on modify { create; }

  validation validateQuantity on save { field Quantity; create; }
  validation validatePrice on save { field Price; create; }
  validation validateItemName on save { field Name; create; }

  side effects
  { field Price affects field _Order.TotalPrice;
    field Quantity affects field _Order.TotalPrice;
}

  mapping for zrap_aitem_dg
    {
      Itemuuid      = itemuuid;
      Orderuuid     = orderuuid;
      Name          = name;
      Price         = price;
      CurrencyCode  = currency_code;
      Quantity      = quantity;
      LastChangedAt = last_changed_at;
    }


}